plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {

    def clientVersion = appVersion
    def buildVersionCode = appBuildNumber
    if (buildVersionCode < 1) {
        buildVersionCode = 1
        def reset = project.hasProperty('RESET')
        //auto-increment version code
        Properties versionProps = new Properties()
        def versionPropsFile = file('version.properties')
        if (versionPropsFile.exists() && versionPropsFile.canRead()) {
            versionProps.load(new FileInputStream(versionPropsFile))
            buildVersionCode = (versionProps['VERSION_CODE'] ?: buildVersionCode.toString()).toInteger()
        }
        buildVersionCode += 1
        if (reset) {
            buildVersionCode = 1
        }
        versionProps['VERSION_CODE'] = buildVersionCode.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        def (majorVersion, minorVersion, patchVersion) = clientVersion.tokenize('.')
        def bvc = majorVersion.padLeft(3, '0') + minorVersion.padLeft(2, '0') + patchVersion.padLeft(2, '0') + String.valueOf(buildVersionCode).padLeft(3, '0')
        buildVersionCode = bvc.toInteger()
    }

    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.example.coba"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode buildVersionCode
        versionName clientVersion
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "APP_SERVICE_BASE_URL", '"https://api.github.com/"')
        buildConfigField("String", "CLIENT_ID", '""')
        buildConfigField("String", "CLIENT_VERSION", '"' + clientVersion + '"')
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            shrinkResources false
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        sit {
            applicationIdSuffix ".sit"
            versionNameSuffix "-sit"
        }

        uat {
            applicationIdSuffix ".uat"
            versionNameSuffix "-uat"
        }

        production {
            dimension "version"
        }
    }

    android {
        compileOptions {
            sourceCompatibility 1.8
            targetCompatibility 1.8
        }
    }

    androidExtensions {
        experimental = true
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        // To check for a build type instead, use variant.buildType.name == "buildType"
        def buildType = variant.buildType.name

        //only provide release version with proguard on when flavors are uat & production
        //if (!names.contains("production") && !names.contains("uat") && buildType != "debug") {
        if ((names.contains("local") && buildType != "debug") || (names.contains("production") && buildType == "debug")) {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation retrofitLibs.values()
    implementation httpLibs.values()
    implementation koinLibs.values()
    implementation hawkLib.core
    implementation glideLibs.core
    implementation coroutineLibs.values()
    implementation androidxLibs.values()
    implementation ankoLibs.values()
    implementation firebaseLibs.values()
    implementation unitTest.values()
    implementation appsflyerLibs.values()
    implementation multidexLibs.values()
    kapt glideLibs.compiler


    implementation 'com.intuit.sdp:sdp-android:1.0.6'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'io.github.inflationx:calligraphy3:3.0.0'
    implementation 'com.tbuonomo.andrui:viewpagerdotsindicator:2.1.2'
    implementation 'com.facebook.shimmer:shimmer:0.4.0'
    implementation "org.jetbrains.anko:anko-commons:0.10.8"
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation materialLibs.values()

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation 'com.chaos.view:pinview:1.4.2'
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }
}